<?xml version="1.0" encoding="utf-8"?>
<mx:TitleWindow xmlns:mx="http://www.adobe.com/2006/mxml"
                layout="vertical"
                isPopUp="false"
                styleName="authWindow"
                creationComplete="creationComplete()"
                title='{resourceManager.getString("ispman", "Authentication Required")}'>
    <mx:Script>
      import mx.managers.PopUpManager;
      import mx.collections.ArrayCollection;

      import mx.events.FlexEvent;

      import mx.rpc.AbstractOperation;
      import mx.rpc.events.FaultEvent;
      import mx.rpc.events.ResultEvent;

      import net.zengrong.logging.Firebug;

      import org.ufsoft.ispman.i18n.Locale;
      import org.ufsoft.ispman.models.AuthenticatedUser;
      import org.ufsoft.ispman.events.AuthenticationEvent;
      import org.ufsoft.ispman.events.TranslationEvent;

      private var loginType     : int=1;

      //[Bindable]
      public var user           : AuthenticatedUser;


      [Bindable]
      public var languagesArray : ArrayCollection;

      protected function creationComplete():void {

        PopUpManager.centerPopUp(this); // center the popup

        // Listen to key events to enable or not the authenticate button
        this.addEventListener(KeyboardEvent.KEY_UP, submit_Ok);

        // listen to login type changes
        loginTypeCombo.addEventListener("change", loginTypeSelect);

        // listen to authenticate button cliks to submit or Enter-key presses
        authButton.addEventListener( MouseEvent.CLICK, submitForm );
        authForm.addEventListener( KeyboardEvent.KEY_DOWN, submitFormEnterKey );

        // Re-login? Select the correct Login type
        if ( user.username != '' ) {
          authButton.enabled = true;
          for ( var i:int=0; i&lt;loginTypeArray.source.length; i++ ) {
            if ( loginTypeArray.getItemAt(i).type ==  user.loginType ) {
              loginTypeCombo.selectedIndex = i;
              break;
            }
          }
          password.setFocus();
        } else {
          // Set focus on username
          username.setFocus();
        }
        //Firebug.debug(this);
        var operation: AbstractOperation=parentApplication.getOperation(
          'locales.get_locales',
          getLanguagesSucess);
        operation.send();
      }

      private function getLanguagesSucess(event:ResultEvent):void {
        languagesArray = event.result as ArrayCollection;
        Firebug.debug(1, Locale.getInstance().getLocale());
        for ( var i:int=0; i&lt;languagesArray.source.length; i++ ) {
            if ( languagesArray.getItemAt(i).locale ==  Locale.getInstance().getLocale() ) {
              languageCombo.selectedIndex = i;
              break;
            }
          }
      }

      private function getLanguagesFailure(event:FaultEvent):void {
        // Hardcoded Array. Might be missing some translations.
        languagesArray = new ArrayCollection([
          {label: 'English', locale: 'en_US'},
          {label: 'PortuguÃªs', locale: 'pt_PT'},
        ]);
      }

      /*
       * Function to change the login type
       */
      private function loginTypeSelect(event:Event):void {
        loginType = ComboBox(event.target).selectedItem.type;
      }

      // Function to check if the authentication button should be enabled or not
      private function submit_Ok(evt:KeyboardEvent):void {
        if ( password.text.length > 0 &amp;&amp; username.text.length > 0) {
          authButton.enabled = true;
          username.errorString=null;
          password.errorString=null;
        } else {
          authButton.enabled = false;
          if ( username.text.length == 0 ) {
            username.errorString="Username cannot be empty!";
          }
          if ( password.text.length == 0 ) {
            password.errorString="Password cannot be empty!";
          }
        }
      }

      // Fire the submit form event
      private function submitForm(evt:MouseEvent):void {
        user = new AuthenticatedUser(username.text, password.text, loginType);
        dispatchEvent(new AuthenticationEvent(AuthenticationEvent.SEND, user));
      }

      private function submitFormEnterKey(evt:KeyboardEvent):void {
        if ( evt.keyCode == 13 &amp;&amp; authButton.enabled ) {
        user = new AuthenticatedUser(username.text, password.text, loginType);
        dispatchEvent(new AuthenticationEvent(AuthenticationEvent.SEND, user));
        }
      }

      private function languageSelectionChanged():void {
        Locale.getInstance().load(languageCombo.selectedItem.locale);
      }

    </mx:Script>

    <mx:Form id="authForm">
        <mx:FormItem id="usernameItem"
                     label='{resourceManager.getString("ispman", "Username:")}'>
            <mx:TextInput id="username" text="{user.username}"/>
        </mx:FormItem>
        <mx:FormItem id="passwordItem"
                     label='{resourceManager.getString("ispman", "Password:")}'>
            <mx:TextInput id="password" displayAsPassword="true"/>
        </mx:FormItem>
        <mx:FormItem id="loginTypeItem"
                     label='{resourceManager.getString("ispman", "Login as:")}'>
            <mx:ComboBox id="loginTypeCombo"
                         labelField="label" change="loginTypeSelect(event)">
                <mx:ArrayCollection id="loginTypeArray">
                    <mx:Object label='{resourceManager.getString("ispman", "Administrator")}' type="1"/>
                    <mx:Object label='{resourceManager.getString("ispman", "Reseller")}' type="2"/>
                    <mx:Object label='{resourceManager.getString("ispman", "Client")}' type="3"/>
                    <mx:Object label='{resourceManager.getString("ispman", "Domain")}' type="4"/>
                </mx:ArrayCollection>
            </mx:ComboBox>
        </mx:FormItem>
        <mx:FormItem id="language"
                     label='{resourceManager.getString("ispman", "Language:")}'>
            <mx:ComboBox id="languageCombo" dataProvider="{languagesArray}"
                         labelField="label" change="languageSelectionChanged()"/>
        </mx:FormItem>
        <mx:Button label='{resourceManager.getString("ispman", "Authenticate")}'
                   id ="authButton" enabled="false" width="100%"/>
        <mx:TextArea width="100%" height="100" textAlign="justify" condenseWhite="true">
            <mx:htmlText><![CDATA[
                <b><p>This system is for the use of authorized users only.
                Individuals using this computer system without authority, or
                in excess of their authority, are subject to having all of
                their activities on this system monitored and recorded.</p>

                <p>In the course of monitoring individuals improperly using this system,
                or in the course of system maintenance, the activities of authorised
                users may also be monitored.</p>

                <p>Anyone using this system expressly consents to such monitoring and is
                advised that if such monitoring reveals possible evidence of criminal
                activity, system personnel may provide the evidence of such monitoring
                to law enforcement officials.</p></b>]]>
            </mx:htmlText>
        </mx:TextArea>
    </mx:Form>
</mx:TitleWindow>

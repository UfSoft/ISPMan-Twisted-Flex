<?xml version="1.0" encoding="utf-8"?>
<mx:Button xmlns:mx="http://www.adobe.com/2006/mxml"
           creationComplete="creationComplete()">
    <mx:Script>
      import flash.utils.Timer;
      import flash.events.TimerEvent;
      import mx.controls.Alert;

      import mx.rpc.AbstractOperation;
      import mx.rpc.events.FaultEvent;
      import mx.rpc.events.ResultEvent;

      import org.ufsoft.ispman.events.LoadDataEvent;
      import org.ufsoft.ispman.events.AuthenticationEvent;

      import net.zengrong.logging.Firebug;

      private const TIMER_INTERVAL : int=30 * 1000; // 30 seconds
      private var t                : Timer;
      private var _operation       : AbstractOperation;

      private function creationComplete():void {
        this.label = resourceManager.getString(
          "ispman", "Processes in Queue: {0}", ['...']);

        t = new Timer(TIMER_INTERVAL);
        t.addEventListener(TimerEvent.TIMER, checkProcesses);
        parentApplication.addEventListener(AuthenticationEvent.SUCESS, startTimer);
        parentApplication.addEventListener(AuthenticationEvent.FAILURE, stopTimer);
        parentApplication.addEventListener(AuthenticationEvent.NEEDED, stopTimer);
        parentApplication.addEventListener(AuthenticationEvent.SUCESS, checkProcesses);
        Firebug.debug("ProcessButton Started!");
      }

      private function startTimer(event:AuthenticationEvent):void {
        t.start();
      }

      private function stopTimer(event:AuthenticationEvent):void {
        t.stop();
      }

      private function checkProcesses(event:Event=null):void {
        if ( ! _operation ) {
          _operation = parentApplication.getOperation('processes.count',
                                                      updateProcesses);
        }
        _operation.send();
      }

      private function updateProcesses(event:ResultEvent):void {
        this.label = resourceManager.getString(
          "ispman", "Processes in Queue: {0}", [event.result]);
      }
    </mx:Script>
</mx:Button>

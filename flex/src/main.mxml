<?xml version="1.0" encoding="utf-8"?>
<mx:Application xmlns:mx="http://www.adobe.com/2006/mxml"
                xmlns:cp="components.*"
                xmlns:controls="org.ufsoft.ispman.controls.*"
                layout="absolute"
                label="ISPMan"
                pageTitle="ISPMan"
                horizontalGap="2"
                width="100%"
                height="100%"
                paddingTop="5" paddingRight="5" paddingBottom="10" paddingLeft="5"
                creationComplete="creationComplete(event)">

    <mx:Style source="style.css" />
    <mx:Script>
      import mx.collections.ArrayCollection;
      import mx.controls.Alert;
      import mx.events.FlexEvent;
      import mx.managers.PopUpManager;
      import flash.display.DisplayObject;

      import mx.messaging.ChannelSet;
      import mx.messaging.channels.SecureAMFChannel;
      import mx.rpc.AbstractOperation;
      import mx.rpc.events.FaultEvent;
      import mx.rpc.events.ResultEvent;
      import mx.rpc.remoting.mxml.RemoteObject;

      import org.ufsoft.ispman.models.AuthenticatedUser;
      import org.ufsoft.ispman.events.LoadDataEvent;
      import org.ufsoft.ispman.events.AuthenticationEvent;
      import components.AuthDialog;
      import components.Overview;
      import components.domains.ManageDomain;
      import components.processes.ProcessesOverview;

      // VARS
      private var authDialog    : AuthDialog;
      private var overview      : Overview;
      private var manageDomain  : ManageDomain;
      private var user          : AuthenticatedUser;
      private var _currentChild : DisplayObject;
      private var processesOverview: ProcessesOverview;

      [Bindable]
      private var availableDomains   : ArrayCollection;

      private function creationComplete(event:FlexEvent) : void {
        addEventListener(AuthenticationEvent.NEEDED, AuthenticationNeeded);
        addEventListener(LoadDataEvent.LOAD, loadDataForEvent);
        addEventListener(AuthenticationEvent.SEND, authenticate);
        addEventListener(AuthenticationEvent.SUCESS, displayOverview);

        // Test with a fake user for existing session
        user = new AuthenticatedUser('', '', 1);
        dispatchEvent(new AuthenticationEvent(AuthenticationEvent.SEND, user));
      }

      private function AuthenticationNeeded(event:Event) : void {
        authDialog = new AuthDialog();
        authDialog.addEventListener(AuthenticationEvent.SEND, authenticate);
        PopUpManager.addPopUp(authDialog, DisplayObject(this), true);
      }

      protected function authenticate(event:AuthenticationEvent) : void {
        user = event.user;
        var remoteObj : RemoteObject=getService();
        var operation : AbstractOperation=remoteObj.getOperation('auth.login');
        operation.addEventListener(FaultEvent.FAULT, authenticateFailure);
        operation.addEventListener(ResultEvent.RESULT, authenticateSuccess);
        operation.send(event.user);
      }

      protected function authenticateFailure(event:FaultEvent) : void {
        dispatchEvent(new AuthenticationEvent(AuthenticationEvent.FAILURE));
        if (event.fault.faultCode == 'AuthenticationNeeded') {
          dispatchEvent(new AuthenticationEvent(AuthenticationEvent.NEEDED));
        } else {
          var errorMsg : String="Authentication Failed " + event.fault.faultCode;
          Alert.show(event.fault.faultDetail, errorMsg);
        }
      }

      protected function authenticateSuccess(event:ResultEvent) : void {
        logout.addEventListener("click", doLogout);
        logout.enabled = true;
        PopUpManager.removePopUp(authDialog);
        dispatchEvent(new AuthenticationEvent(AuthenticationEvent.SUCESS));
      }

      private function switchContent(content:DisplayObject): void {
        if ( _currentChild ) {
          mainContent.removeChild(_currentChild);
        }
        _currentChild = content;
        mainContent.addChild(content);
      }

      protected function displayOverview(event:Event):void {
        overview = new Overview();
        switchContent(overview);
        chooseDomain.selectedIndex = -1;
      }

      protected function doLogout(event:Event) : void {
        var remoteObj : RemoteObject=getService();
        var operation : AbstractOperation=remoteObj.getOperation('auth.logout');
        operation.addEventListener(ResultEvent.RESULT, doLogoutSucess);
        operation.addEventListener(FaultEvent.FAULT, onServiceFault);
        operation.send();
      }

      protected function doLogoutSucess(event:ResultEvent) : void {
        dispatchEvent(new AuthenticationEvent(AuthenticationEvent.NEEDED));
        logout.enabled = false;
      }

      private function loadDataForEvent(event:LoadDataEvent) : void {
        var remoteObj : RemoteObject=getService();
        var operation : AbstractOperation=remoteObj.getOperation(event.endpoint);
        operation.addEventListener(ResultEvent.RESULT, loadDataForEventSucess);
        operation.addEventListener(FaultEvent.FAULT, onServiceFault);
        operation.send();
      }

      private function loadDataForEventSucess(event:ResultEvent) : void {
        availableDomains = event.result as ArrayCollection;
        /*var items:Array = new Array();
        for each ( var item:Object in event.result ) {
            items.push(item);
        }
        availableDomains = new ArrayCollection(items);*/
      }

      /*
       * Function to load the selected domain panel
       */
      private function domainChange(event:Event):void {
        manageDomain = new ManageDomain();
        switchContent(manageDomain);
      }

      private function loadProcessesOverview(event:Event):void {
        processesOverview = new ProcessesOverview();
        switchContent(processesOverview);
      }

      public function getService() : RemoteObject {
        var url : String='https://{server.name}:{server.port}/service';
        var channel : SecureAMFChannel=new SecureAMFChannel("pyamf-channel", url);
        // Create a channel set and add your channel(s) to it
        var channels : ChannelSet=new ChannelSet();
        channels.addChannel(channel);

        // Create a new remote object and set channels
        var remoteObject : RemoteObject=new RemoteObject("ISPManService");

        remoteObject.showBusyCursor = true;
        remoteObject.channelSet = channels;
        remoteObject.addEventListener(FaultEvent.FAULT, onServiceFault);
        return remoteObject;
      }

      /**
       * Service reported an error.
       *
       * @param event Event containing error information.
       */
      protected function onServiceFault(event : FaultEvent) : void {
        if (event.fault.faultCode == 'AuthenticationNeeded') {
          dispatchEvent(new Event("AuthenticationNeeded", true));
        } else {
          var errorMsg : String=event.fault.faultCode;
          Alert.show(event.fault.faultDetail, errorMsg);
        }
      }

    </mx:Script>
    <mx:ApplicationControlBar width="100%" dock="true" id="appCB">
        <mx:HBox width="100%">
            <mx:Button id="overviewButton" label="Overview" click="displayOverview(event)"/>
            <mx:ComboBox id="chooseDomain" prompt="Choose Domain"
                         labelField="label" dataProvider="{availableDomains}"
                         change="domainChange(event)"/>
            <mx:Spacer width="100%"/>
            <controls:ProcessesButton click="loadProcessesOverview(event)"/>
            <mx:Button id="ispman_home" label="ISPMan Home"/>
            <mx:Button id="logout" label="Logout"/>
        </mx:HBox>
    </mx:ApplicationControlBar>
    <mx:HDividedBox id="mainContent" width="100%" height="100%" cornerRadius="5"
                    paddingTop="5" paddingRight="5" paddingBottom="5" paddingLeft="5">
    </mx:HDividedBox>
</mx:Application>
